import org.apache.tools.ant.filters.*

plugins {
  id 'java'
  id 'eclipse'
  id 'idea'
  id 'com.github.johnrengelman.shadow' version '1.2.4'
}

group = 'com.codeski'
version = '1.5.1-SNAPSHOT'
sourceCompatibility = '1.8'

task filter(type: Copy) {
  from 'src/main/java'
  into 'build/sources'
  filter ReplaceTokens, tokens: [
    version: project.version
  ]
}

compileJava {
  source = 'build/sources'
}
compileJava.dependsOn filter

processResources {
  filter ReplaceTokens, tokens: [
    version: project.version
  ]
}

dependencies {
  compile 'org.bukkit:bukkit:1.12-R0.1-SNAPSHOT'
  compile 'org.bstats:bstats-bukkit:1.1'
  compile 'org.dynmap:DynmapCoreAPI:1.9.4'
  compile 'org.kitteh:VanishNoPacket:3.19.1'
}

repositories {
  maven { url 'https://hub.spigotmc.org/nexus/content/groups/public/' }
  maven { url 'http://repo.bstats.org/content/repositories/releases/' }
  maven { url 'http://repo.mikeprimm.com/' }
  maven { url 'http://repo.kitteh.org/content/groups/public/' }
}

shadowJar {
  dependencies {
    include(dependency('org.bstats:bstats-bukkit'))
  }
  relocate 'org.bstats', 'com.codeski'
}

jar.finalizedBy(shadowJar);
